maxNumber = 3
Number = {0..maxNumber}
channel display, serve, ticket : Number
channel next
channel enter, leave

TicketMachine(x) = ticket.x -> TicketMachine((x+1)%(maxNumber+1))

Display(x) = display.x -> next -> Display((x+1)%(maxNumber+1))

Clerk = serve?x -> next -> Clerk

Client = enter -> ticket?x -> display.x -> serve.x -> next -> leave -> Client

ticketsAlpha = {| ticket.n | n <- Number |}

Office = (TicketMachine(0)[| ticketsAlpha |] Client)
       	 [| {| next, serve.n, display.n | n <- Number |}  |]
	 (Display(0) [| {| next |} |] Clerk)

Clerk' = display?x -> serve.x -> next -> Clerk'

Office' = (TicketMachine(0) [| ticketsAlpha |] Client)
       	 [| {| next, serve.n, display.n | n <- Number |}  |]
	 (Display(0) [| {| next, display.n | n <- Number |} |] Clerk')

assert Office' :[deadlock free]

Office'' = (TicketMachine(0) [| ticketsAlpha |] Client)
       	 [| {| next, serve.n | n <- Number |}  |]
	 (Display(0) [| {| next, display.n | n <- Number |} |] Clerk')


MultiClientOffice = (TicketMachine(0) [| ticketsAlpha |] (Client ||| Client))
       	 [| {| next, serve.n, display.n | n <- Number |}  |]
	 (Display(0) [| {| next, display.n | n <- Number |} |] Clerk')

CanLeaveIfEnters = enter -> leave -> CanLeaveIfEnters

CanLeaveIfEntersMulti(x) = (x < maxNumber) & enter -> CanLeaveIfEntersMulti(x+1)
			[] (x > 0) & leave -> CanLeaveIfEntersMulti(x-1)

assert CanLeaveIfEnters [FD= Office \ {| display.n, serve.n, ticket.n, next | n <- Number |}
assert CanLeaveIfEnters [FD= Office' \ {| display.n, serve.n, ticket.n, next | n <- Number |}
assert CanLeaveIfEnters [FD= Office'' \ {| display.n, serve.n, ticket.n, next | n <- Number |}
assert CanLeaveIfEntersMulti(0) [FD= MultiClientOffice \ {| display.n, serve.n, ticket.n, next | n <- Number |}